// This jenkinsfile is used to run CI/CD on my local (Windows) box, no VM's needed.
def SPIRA_PASS = 2
def SPIRA_FAIL = 1

node {

  def results = ["Acceptance Tests": ["status": 0, "id": 35], 
                 "Dependency Check": ["status": 0, "id" :39],
                 "API Tests": ["status": 0, "id": 40],
                 "UI BDD Tests": ["status": 0, "id": 41],
                 "UI Tests": ["status": 0, "id": 42],
                 "Performance Tests": ["status":0, "id": 44]]

  stage ('Scripted Pipeline: Checkout Code') {
    cleanWs()
    checkout scm
  }

  // build the war file (the binary).  This is the only
  // place that happens.
  stage('Build') {
    sh './gradlew clean assemble'
  }

  // run all the unit tests - these do not require anything else
  // to be running and most run very quickly.
  stage('Unit Tests') {
    sh(script: './gradlew test')
    junit 'build/test-results/test/*.xml'
  }

  // run the tests which require connection to a
  // running database.
  stage('Database Tests') {
    sh './gradlew integrate'
    junit 'build/test-results/integrate/*.xml'
  }

  // These are the Behavior Driven Development (BDD) tests
  // See the files in src/bdd_test
  // These tests do not require a running system.
  stage('Automated Acceptance Tests') {
      result = sh(script: './gradlew generateCucumberReports', returnStatus: true)
      results["Acceptance Tests"]["status"] = (result == 0) ? SPIRA_PASS : SPIRA_FAIL
      // generate the code coverage report for jacoco
      sh './gradlew jacocoTestReport'
      junit 'build/test-results/bdd/*.xml'
  }

  // Runs an analysis of the code, looking for any
  // patterns that suggest potential bugs.
  stage('Static Analysis') {
      sh './gradlew sonarqube'
      // wait for sonarqube to finish its analysis
      sleep 5
      sh './gradlew checkQualityGate'
  }


  // Move the binary over to the test environment and
  // get it running, in preparation for tests that
  // require a whole system to be running.
  stage('Deploy to test') {
    // if running locally, add path to tomcat e.g.: -Pdeploy_directory=\"/Users/<user>/Downloads/apache-tomcat-9.0.37/webapps\"
    sh './gradlew deployToTestWindowsLocal'
    // pipenv needs to be installed and on the path for this to work.
    sh 'PIPENV_IGNORE_VIRTUALENVS=1 pipenv install'

    // Wait here until the server tells us it's up and listening
    sh './gradlew waitForHeartBeat'
  }


  // Run the tests which investigate the functioning of the API.
  stage('API Tests') {
    result = sh(script: './gradlew runApiTests', returnStatus: true)
    results["API Tests"]["status"] = (result == 0) ? SPIRA_PASS : SPIRA_FAIL
    junit 'build/test-results/api_tests/*.xml'
  }

  // We use a BDD framework for some UI tests, Behave, because Python rules
  // when it comes to experimentation with UI tests.  You can try things and see how they work out.
  // this set of BDD tests does require a running system.
  // BDD at the UI level is just to ensure that basic capabilities work,
  // not that every little detail of UI functionality is correct.  For
  // that purpose, see the following stage, "UI Tests"
  stage('UI BDD Tests') {
    sh './gradlew runBehaveTests'
    result = sh(script: './gradlew generateCucumberReport', returnStatus: true)
    results["UI BDD Tests"]["status"] = (result == 0) ? SPIRA_PASS : SPIRA_FAIL
    junit 'build/test-results/bdd_ui/*.xml'
  }

  // This set of tests investigates the functionality of the UI.
  // Note that this is separate fom the UI BDD Tests, which
  // only focuses on essential capability and therefore only
  // covers a small subset of the possibilities of UI behavior.
  stage('UI Tests') {
    result = sh(script: 'cd src/ui_tests/java && ./gradlew clean test', returnStatus: true)
    results["UI Tests"]["status"] = (result == 0) ? SPIRA_PASS : SPIRA_FAIL
    junit 'src/ui_tests/java/build/test-results/test/*.xml'    
  }

  // Run OWASP's "DependencyCheck". https://owasp.org/www-project-dependency-check/
  // You are what you eat - and so it is with software.  This
  // software consists of a number of software by other authors.
  // For example, for this project we use language tools by Apache,
  // password complexity analysis, and several others.  Each one of
  // these might have security bugs - and if they have a security
  // bug, so do we!
  //
  // DependencyCheck looks at the list of known
  // security vulnerabilities from the United States National Institute of
  // Standards and Technology (NIST), and checks if the software
  // we are importing has any major known vulnerabilities. If so,
  // the build will halt at this point.
  stage('Analyze security of dependencies') {
    result = sh(script: './gradlew dependencyCheckAnalyze', returnStatus: true)
    results["Dependency Check"]["status"] = (result == 0) ? SPIRA_PASS : SPIRA_FAIL
  }

  // Run Jmeter performance testing https://jmeter.apache.org/
  // This test simulates 50 users concurrently using our software
  // for a set of common tasks.
  stage('Testing performance') {
    result = sh(script: './gradlew runPerfTests', returnStatus: true)
    results["Performance Tests"]["status"] = (result == 0) ? SPIRA_PASS : SPIRA_FAIL
  }

  // Runs mutation testing against some subset of our software
  // as a spot test.  Mutation testing is where bugs are seeded
  // into the software and the tests are run, and we see which
  // tests fail and which pass, as a result.
  //
  // what *should* happen is that where code or tests are altered,
  // the test should fail, shouldn't it? However, it sometimes
  // happens that no matter how code is changed, the tests
  // continue to pass, which implies that the test wasn't really
  // providing any value for those lines.
  stage('Mutation testing') {
    sh './gradlew pitest'
  }

  stage('Send results to spira') {
    withCredentials([usernamePassword(credentialsId: 'spira-api-token', passwordVariable: 'token', usernameVariable: 'user')]) {

      results.each { t, i ->
        def extra_details = ''
        if (t == 'Dependency Check') {
          extra_details += '\nReport URL: http://localhost:8080/workspace/Demo/build/reports/dependency-check-report.html'
        } else if (t == 'UI BDD Tests') {
          extra_details += '\nReport URL: http://localhost:8080/workspace/Demo/build/reports/bdd/cucumber-html-reports/overview-features.html'
        }
        sh """PIPENV_IGNORE_VIRTUALENVS=1 pipenv run python collect-results-for-spira.py --token=$token --user=$user \
        --caseid=${i["id"]} --status=${i["status"]} --buildName='$t' \
        --message='See Jenkins for build log' \
        --details='Updated by $JOB_BASE_NAME Build #$BUILD_NUMBER\nBuild: $BUILD_URL\n Job: ${JOB_DISPLAY_URL}${extra_details}'"""
      }
    }
  }
  stage('Build documentation') {
    sh './gradlew javadoc'
  }
}